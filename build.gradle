buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url "https://artifactory.prezi.com/plugins-snapshot"
		}
	}
	dependencies {
		classpath "com.prezi.gradle:gradle-prezi-plugin:2.0-SNAPSHOT"
	}
}

allprojects {
	group = 'com.prezi.spaghetti'

	apply plugin: 'idea'
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'maven'
	apply plugin: 'prezi'

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	repositories {
		mavenLocal()
		maven preziDownloadRepo("repo")
	}

	dependencies {
		testCompile "junit:junit:4.11"
		testCompile "org.mockito:mockito-all:1.9.5"
		testCompile("org.spockframework:spock-core:0.7-groovy-1.8") {
			exclude group: "org.codehaus.groovy"
		}
	}

	if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
		def user = project.getProperty("nexusUser")
		def password = project.getProperty("nexusPassword")
		uploadArchives {
			repositories {
				repositories {
					mavenDeployer {
						repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
							authentication(userName: user, password: password)
						}
						snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
							authentication(userName: user, password: password)
						}
					}
				}
			}
		}
	}
}

configure(subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }) {
	dependencies {
		compile project(path: ":spaghetti-core")
	}
}

project(":gradle-spaghetti-plugin") {
	dependencies {
		compile gradleApi()
		compile "com.google.javascript:closure-compiler:v20131014"
		compile project(path: ":spaghetti-core")
		// Add all support projects
		subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }.each { supportProject ->
			compile project(path: supportProject.path)
		}
	}

	task run(type: Exec) {
		workingDir "src/test/at"
		commandLine = ["gradle", "clean", "packApplication", "-is"]
	}
}
