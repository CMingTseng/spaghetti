def gradleVer = "2.0"

String gitVersion
if (hasProperty("release")) {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--dirty"].execute().text.trim()
} else {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--abbrev=0"].execute().text.trim() + "-SNAPSHOT"
}

task version << {
	println "Version: ${gitVersion}"
}

def sonatype = hasProperty("sonatype")
def signArtifacts = sonatype || hasProperty("sign")

allprojects {
	group = 'com.prezi.spaghetti'
	version = gitVersion
	apply plugin: 'idea'

	tasks.withType(Upload).all { dependsOn "check" }
	tasks.withType(org.gradle.api.tasks.compile.JavaCompile) {
		options.compilerArgs << "-Xlint:unchecked"
	}
}

subprojects { subproject ->
	apply plugin: 'maven'

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://gradle.artifactoryonline.com/gradle/libs"
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				pom.project {
					url "http://github.com/prezi/spaghetti/"
					name subproject.name
					description subproject.description

					scm {
						url "http://github.com:prezi/spaghetti"
						connection "scm:git@github.com:prezi/spaghetti.git"
						developerConnection "scm:git@github.com:prezi/spaghetti.git"
					}

					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}

					developers {
						developer {
							id "lptr"
							name "Lorant Pinter"
						}
						developer {
							id "exFalso"
							name "Andras Slemmer"
						}
						developer {
							id "gidapataki"
							name "Gida Pataki"
						}
					}
				}

				if (sonatype) {
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
				} else if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
					def user = project.getProperty("nexusUser")
					def password = project.getProperty("nexusPassword")
					repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
						authentication(userName: user, password: password)
					}
				}
			}
		}
	}

	if (signArtifacts) {
		apply plugin: "signing"

		signing {
			sign configurations.archives
		}

		uploadArchives {
			repositories {
				mavenDeployer {
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				}
			}
		}
	}
}

ext.codeProjects = subprojects.findAll {
			it.name == "spaghetti-core" || it.name ==~ /gradle-.*/ || it.name ==~ /spaghetti-.*-support/
		}
ext.groovyProjects = [ project(":spaghetti-haxe-support"), project(":spaghetti-typescript-support") ]
ext.javaProjects = codeProjects - groovyProjects

configure(codeProjects) {
	apply plugin: 'groovy'

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	dependencies {
		testCompile "junit:junit:4.11"
		testCompile "org.mockito:mockito-all:1.9.5"
		testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
			exclude group: "org.codehaus.groovy"
		}
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	jar {
		from rootProject.file("LICENSE.txt")
	}

	artifacts {
		archives jar
		archives sourcesJar
	}
}

subprojects {
	String[] apis = [
		"http://docs.oracle.com/javase/8/docs/api/",
		"http://groovy.codehaus.org/gapi/",
		"http://www.gradle.org/docs/${gradleVer}/javadoc/"
	]

	tasks.withType(Javadoc) {
		options.encoding = "UTF-8"
		options.links apis
		options.noTimestamp = true
	}

	tasks.withType(Groovydoc) {
		groovydoc.groovyClasspath = configurations.compile
		// Done in doFirst() so that we don't download stuff in configuration phase
		doFirst {
			apis.each {
				link(it, *(new URL("$it/package-list").text.tokenize("\n")))
			}
		}
	}
}

configure(javaProjects) {
	task javadocJar(type: Jar) {
		dependsOn javadoc
		classifier = "javadoc"
		from javadoc.destinationDir
	}

	artifacts {
		archives javadocJar
	}
}

configure(groovyProjects) {
	task groovydocJar(type: Jar) {
		dependsOn groovydoc
		classifier = "javadoc"
		from groovydoc.destinationDir
	}

	artifacts {
		archives groovydocJar
	}
}

def inheritJavadoc(Project target, Project dependency) {
	def coreJavadoc = dependency.tasks.javadoc
	def dependencyUrl = "http://prezi.github.io/spaghetti/javadoc/" + dependency.name

	target.javadoc {
		dependsOn coreJavadoc
		options.linksOffline dependencyUrl, coreJavadoc.destinationDir.path
	}

	target.groovydoc {
		dependsOn coreJavadoc
		doFirst {
			link(dependencyUrl, *(new File(coreJavadoc.destinationDir, "package-list").text.tokenize("\n")))
		}
	}
}

// Make Javadocs depend on core
configure(codeProjects - project(":spaghetti-core")) { p ->
	inheritJavadoc p, project(":spaghetti-core")
}

configure(subprojects.findAll { it.name ==~ /gradle-spaghetti-.*-plugin/}) { p ->
	inheritJavadoc p, project(":gradle-spaghetti-plugin")
}

project(":spaghetti-core") {
	dependencies {
		compile "com.google.javascript:closure-compiler:v20140508"
	}
}

configure(subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }) {
	dependencies {
		compile project(path: ":spaghetti-core")
		testCompile project(path: ":spaghetti-core", configuration: "testCompile")
	}
}

project(":gradle-spaghetti-plugin") {
	dependencies {
		compile gradleApi()
		compile project(path: ":spaghetti-core")

		// Required only for the incubating stuff brought over from Gradle 2.0
		compile "commons-lang:commons-lang:2.6"
		compile "commons-io:commons-io:2.4"

		// Add all support projects
		rootProject.subprojects.findAll { it.name ==~ /spaghetti-.*-support/ }.each { supportProject ->
			compile project(path: supportProject.path)
		}
	}

	task run(type: Exec) {
		// Make sure we install all dependencies
		dependsOn install
		configurations.runtime.allDependencies.withType(ProjectDependency).all { ProjectDependency projectDep ->
			dependsOn projectDep.dependencyProject.getTasksByName("install", false)
		}
		workingDir "src/test/at"
		commandLine = ["gradle", "clean", "packApplication", "-is", "--no-daemon"]
	}
}

configure(subprojects.findAll { it.name ==~ /gradle-spaghetti-.*-plugin/}) {
	dependencies {
		compile gradleApi()
		compile project(path: ":gradle-spaghetti-plugin")
	}
}

project(":gradle-spaghetti-haxe-plugin") {
	dependencies {
		compile "com.prezi.haxe:gradle-haxe-plugin:4.1.1"
		compile "org.webjars:requirejs:2.1.8"
	}
}

project(":gradle-spaghetti-typescript-plugin") {
	dependencies {
		compile "com.prezi.typescript:gradle-typescript-plugin:2.2.1"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleVer
}
