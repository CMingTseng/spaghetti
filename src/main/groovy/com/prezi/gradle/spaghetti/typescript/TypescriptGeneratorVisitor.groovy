package com.prezi.gradle.spaghetti.typescript

import com.prezi.gradle.spaghetti.FQName
import com.prezi.gradle.spaghetti.ModuleConfiguration
import com.prezi.gradle.spaghetti.ModuleDefinition
import org.antlr.v4.runtime.Token
import prezi.spaghetti.SpaghettiModuleBaseVisitor
import prezi.spaghetti.SpaghettiModuleParser

/**
 * Created by lptr on 16/11/13.
 */
abstract class TypescriptGeneratorVisitor<T> extends SpaghettiModuleBaseVisitor<Object> {
	static def TYPESCRIPT_TYPE_NAME_CONVERSION = [
			(ModuleConfiguration.TYPE_VOID): "void",
			(ModuleConfiguration.TYPE_BOOL): "boolean",
			(ModuleConfiguration.TYPE_INT): "number",
			(ModuleConfiguration.TYPE_FLOAT): "number",
			(ModuleConfiguration.TYPE_STRING): "string"
	]

	protected final ModuleConfiguration config
	protected final ModuleDefinition module
	protected final File outputDirectory

	protected File currentFile

	protected TypescriptGeneratorVisitor(ModuleConfiguration config, ModuleDefinition module, File outputDirectory)
	{
		this.config = config
		this.module = module
		this.outputDirectory = outputDirectory
	}

	protected File createTypescriptSourceFile(String name)
	{
		return createTypescriptSourceFile(name, module.name, outputDirectory)
	}

	public static File createTypescriptSourceFile(FQName fqName, File outputDirectory) {
		return createTypescriptSourceFile(fqName.localName, fqName, outputDirectory)
	}

	public static File createTypescriptSourceFile(String name, FQName baseName, File outputDirectory) {
		def packageDir = baseName.createNamespacePath(outputDirectory)
		packageDir.mkdirs()
		def file = new File(packageDir, name + ".ts")
		file.delete()
		file << "/*\n"
		file << " * Generated by Spaghetti.\n"
		file << " */\n"
		if (baseName.hasNamespace())
		{
			file << "module ${baseName.fullyQualifiedName} {\n"
			file << "\n"
		}
		return file
	}

	protected String typescriptTypeName(SpaghettiModuleParser.FqNameContext typeNameContext)
	{
		def typeName = FQName.fromContext(typeNameContext)
		def fqName = config.resolveTypeName(typeName, module.name)
		return TYPESCRIPT_TYPE_NAME_CONVERSION.get(fqName) ?: fqName.fullyQualifiedName
	}

	protected void addDocumentationIfNecessary(Token doc)
	{
		def documentation = doc?.text
		if (documentation != null)
		{
			currentFile << documentation
		}
	}
}
