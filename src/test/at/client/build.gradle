import com.prezi.gradle.spaghetti.BundleApplication
import com.prezi.gradle.spaghetti.ExtractModules
import com.prezi.gradle.spaghetti.GenerateClient

apply plugin: "spaghetti"

configurations {
	modules
}

dependencies {
	modules project(path: ":adder", configuration: "modules")
}

task generateClient(type: GenerateClient) {
	configuration configurations.modules
	platform "haxe"
	outputDirectory "$buildDir/haxe"
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateClient
	configuration configurations.modules
	targetPlatform "js"
	source generateClient.outputDirectory
	source "src"
	main "prezi.test.client.Client"
	outputFile "${buildDir}/app-standalone.js"
}

task bundleApplication(type: BundleApplication) {
	dependsOn compileHaxe
	configuration configurations.modules
	inputFile compileHaxe.outputFile
	outputFile "${buildDir}/app.js"
}

task packApplication(type: ExtractModules) {
	dependsOn bundleApplication

	configuration configurations.modules
	def testWebappDir = file("${buildDir}/webapp")
	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
