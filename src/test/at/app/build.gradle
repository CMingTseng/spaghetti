evaluationDependsOn(':text-renderer')

apply plugin: "spaghetti"

configurations {
	modules
}

dependencies {
	modules project(path: ":text-renderer", configuration: "modules")
}

/**
 * Generate type definitions for every client
 */
task generateClient(type: com.prezi.gradle.spaghetti.GenerateClient) {
	configuration configurations.modules
	platform "haxe"
	outputDirectory "$buildDir/haxe"
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateClient
	configuration configurations.modules
	targetPlatform "js"
	source generateClient.outputDirectory
	source "src"
	main "prezi.test.client.Client"
	outputFile "${buildDir}/app-standalone.js"
}

task bundleApplication(type: com.prezi.gradle.spaghetti.BundleApplication) {
	dependsOn compileHaxe
	configuration configurations.modules
	platform "haxe"
	inputFile compileHaxe.outputFile
	outputFile "${buildDir}/app.js"
}

task packApplication(type: com.prezi.gradle.spaghetti.ExtractModules) {
	dependsOn bundleApplication

	configuration configurations.modules
	def testWebappDir = file("${buildDir}/webapp")
	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
