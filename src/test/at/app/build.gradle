import com.prezi.gradle.spaghetti.GenerateApplication

evaluationDependsOn(':text-renderer')

apply plugin: "spaghetti"

configurations {
	modules
}

dependencies {
	modules project(path: ":text-renderer", configuration: "modules")
}

spaghetti {
	platform "haxe"
	configuration configurations.modules
}

/**
 * Generate type definitions for every client
 */
task generateApplication(type: GenerateApplication) {
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateApplication
	configuration configurations.modules
	targetPlatform "js"
	source generateApplication.outputDirectory
	source "src"
	main "prezi.test.client.Client"
	outputFile "${buildDir}/app-standalone.js"
}

task bundleApplication(type: com.prezi.gradle.spaghetti.BundleApplication) {
	dependsOn compileHaxe
	inputFile compileHaxe.outputFile
}

task packApplication(type: com.prezi.gradle.spaghetti.ExtractModules) {
	dependsOn bundleApplication

	configuration configurations.modules
	def testWebappDir = file("${buildDir}/webapp")
	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
