evaluationDependsOn(':text-renderer')

apply plugin: "spaghetti"

dependencies {
	modules project(path: ":text-renderer", configuration: "modules")
}

spaghetti {
	platform "haxe"
}

/**
 * Generate type definitions for every client
 */
task generateApplication(type: com.prezi.gradle.spaghetti.GenerateApplication) {
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateApplication
	configuration configurations.modules
	targetPlatform "js"
	source generateApplication.outputDirectory
	source "src"
	main "prezi.test.client.Client"
}

task bundleApplication(type: com.prezi.gradle.spaghetti.BundleApplication) {
	dependsOn compileHaxe
	inputFile compileHaxe.outputFile
}

task packApplication(type: com.prezi.gradle.spaghetti.ExtractModules) {
	dependsOn bundleApplication

	def testWebappDir = file("${buildDir}/webapp")
	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
