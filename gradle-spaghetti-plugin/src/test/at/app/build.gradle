import com.prezi.spaghetti.gradle.BundleApplication
import com.prezi.spaghetti.gradle.ExtractModules
import com.prezi.spaghetti.gradle.GenerateApplication
import com.prezi.spaghetti.gradle.ObfuscateBundle

evaluationDependsOn(':text-renderer')

apply plugin: "spaghetti"
apply plugin: "haxe"

configurations {
	requirejs
	testlibs { extendsFrom modules }
}

dependencies {
	modules project(path: ":text-renderer", configuration: "modules")
	modulesObf project(path: ":text-renderer", configuration: "modulesObf")
	requirejs "org.webjars:requirejs:2.1.8"

	testlibs group: "haxelib3", name: "mconsole", version: "1.4.0"
	testlibs group: "haxelib3", name: "mcover", version: "2.0.3"
	testlibs group: "haxelib3", name: "mlib", version: "2.0.2"
	testlibs group: "haxelib3", name: "munit", version: "2.0.2"
	testlibs group: "haxelib3", name: "hamcrest", version: "1.2.1"
	testlibs group: "haxelib3", name: "tink_core", version: "1.0.0-alpha"
	testlibs group: "haxelib3", name: "tink_macro", version: "0.0.0-alpha"
	testlibs group: "haxelib3", name: "mockatoo", version: "2.1.0"
}

spaghetti {
	platform "haxe"
}

/**
 * Generate type definitions for every client
 */
task generateApplication(type: GenerateApplication) {
	// This is deprecated, left here to test backwards compatibility
	namespace "prezi.test.client"
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateApplication
	configuration configurations.modules
	targetPlatform "js"
	source generateApplication.outputDirectory
	source "src"
	main "prezi.test.client.Client"
}

task testHaxe(type: com.prezi.gradle.haxe.MUnit) {
	configuration configurations.testlibs
	test compileHaxe
	source "test"
}

task bundleApplication(type: BundleApplication) {
	dependsOn compileHaxe
	inputFile compileHaxe.outputFile
}

task obfuscate(type: ObfuscateBundle) {
	dependsOn bundleApplication
	inputFile bundleApplication.outputFile
	withClosure()
}

def testWebappDir = file("${buildDir}/webapp")

task unzipRequireJS << {
	testWebappDir.mkdirs()
	configurations.requirejs.files.each { zipFile ->
		zipTree(zipFile).each { file ->
			if (file.name == "require.js") {
				def output = new File(testWebappDir, "require.js")
				output << file.text
			}
		}
	}
}

task packApplication(type: ExtractModules) {
	dependsOn bundleApplication
	dependsOn unzipRequireJS

	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
