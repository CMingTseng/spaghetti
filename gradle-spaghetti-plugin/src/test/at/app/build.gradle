import com.prezi.spaghetti.gradle.BundleApplication
import com.prezi.spaghetti.gradle.ExtractModules
import com.prezi.spaghetti.gradle.GenerateApplication

evaluationDependsOn(':text-renderer')

apply plugin: "spaghetti"

configurations {
	requirejs
}

dependencies {
	modules project(path: ":text-renderer", configuration: "modules")
	requirejs "org.webjars:requirejs:2.1.8"
}

spaghetti {
	platform "haxe"
}

/**
 * Generate type definitions for every client
 */
task generateApplication(type: GenerateApplication) {
	namespace "prezi.test.client"
}

task compileHaxe(type: com.prezi.gradle.haxe.CompileHaxe) {
	dependsOn generateApplication
	configuration configurations.modules
	targetPlatform "js"
	source generateApplication.outputDirectory
	source "src"
	main "prezi.test.client.Client"
}

task bundleApplication(type: BundleApplication) {
	dependsOn compileHaxe
	inputFile compileHaxe.outputFile
}

def testWebappDir = file("${buildDir}/webapp")

task unzipRequireJS << {
	testWebappDir.mkdirs()
	configurations.requirejs.files.each { zipFile ->
		zipTree(zipFile).each { file ->
			if (file.name == "require.js") {
				def output = new File(testWebappDir, "require.js")
				output << file.text
			}
		}
	}
}

task packApplication(type: ExtractModules) {
	dependsOn bundleApplication
	dependsOn unzipRequireJS

	outputDirectory testWebappDir

	doLast {
		copy {
			from bundleApplication.outputFile
			into testWebappDir
		}
		copy {
			from "test"
			into testWebappDir
		}
	}
}
