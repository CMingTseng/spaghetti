import com.prezi.spaghetti.gradle.BundleModule
import com.prezi.spaghetti.gradle.ObfuscateBundle

evaluationDependsOn(':layout')

dependencies {
	modules project(path: ":layout", configuration: "modules")
	modulesObf project(path: ":layout", configuration: "modulesObf");
}

spaghetti {
	platform "typescript"
}

task compileTsc(type: TypeScriptTask) {
	dependsOn generateHeaders
	source generateHeaders.outputDirectory
	source "src/main/ts"
	dest = file("${buildDir}/module.js");
}

task bundleModule(type: BundleModule) {
	dependsOn compileTsc
	inputFile compileTsc.dest
}

task obfuscate(type: ObfuscateBundle) {
	dependsOn bundleModule
	inputFile bundleModule.outputFile
}

artifacts {
	archives(bundleModule.outputFile) {
		builtBy bundleModule
	}
	modules(bundleModule.outputFile) {
		builtBy bundleModule
	}
	modulesObf(obfuscate.outputFile) {
		builtBy obfuscate
	}
}

class TypeScriptTask extends SourceTask {
	@OutputFile
	def dest;
	def jsSrcs = [];

	def appendJs(s) {
		jsSrcs.add(new File(s));
		inputs.file(new File(s));
	}

	@TaskAction
	def run() {
		println "Executing TypeScriptTask"
		def typeScriptTemp = new File(project.buildDir, "typescript-output.js");

		def command = ["tsc", "--out", typeScriptTemp] + source.files;
		println command.join(" ");
		def process = command.execute()
		process.waitForProcessOutput(System.out, System.err);

		if (process.exitValue() != 0) {
			throw new RuntimeException("Failed TSC build: " + process.exitValue())
		}

		ant.concat(destfile: (dest as File).canonicalPath, fixlastline: 'yes') {
            jsSrcs.each {
                fileset(file: it)
            }
			fileset(file: typeScriptTemp);
        }
	}
}
