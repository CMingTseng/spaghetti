buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url "https://artifactory.prezi.com/plugins-snapshot"
		}
	}
	dependencies {
		classpath "org.antlr:antlr4:4.2"
	}
}

dependencies {
	compile localGroovy()
	compile "org.antlr:antlr4-runtime:4.2"
	compile "com.google.guava:guava:15.0"
}

def generatedGrammar = file("${buildDir}/antlr-generated")
task generateGrammar(type: JavaExec) {
	def grammarFile = file("src/main/antlr/Module.g4")
	inputs.file grammarFile
	outputs.dir generatedGrammar
	main = "org.antlr.v4.Tool"
	args = [
			"-o", "${generatedGrammar}/com/prezi/spaghetti/grammar",
			"-package", "com.prezi.spaghetti.grammar",
			"-no-listener",
			"-visitor",
			grammarFile
	]
	classpath = buildscript.configurations.classpath
}

processResources {
	def version = project.version =~ /^(\d+(?:[\.]\d+)+)-.*/

	filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [ version: version[0][1] ])
}

idea.module {
	sourceDirs += generatedGrammar
	excludeDirs -= project.buildDir
	if (project.buildDir.exists()) {
		project.buildDir.eachDir() {
			if (it != generatedGrammar) {
				excludeDirs += it
			}
		}
	}
}

sourceSets {
    main {
        java {
            srcDir generatedGrammar
        }
    }
}

compileJava {
	dependsOn generateGrammar
}

task run(type: Exec) {
	workingDir "src/test/at"
	commandLine = ["gradle", "clean", "packApplication", "-is"]
}
