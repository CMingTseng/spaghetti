configurations {
	antlr
}

dependencies {
	compile localGroovy()
	compile "org.antlr:antlr4-runtime:4.2"
	compile "com.google.guava:guava:15.0"
	compile "org.slf4j:slf4j-api:1.7.7"
	antlr "org.antlr:antlr4:4.2"
	testCompile "org.slf4j:slf4j-simple:1.7.7"
}

def generatedGrammar = file("${buildDir}/antlr-generated")
task generateGrammar(type: JavaExec) {
	def grammarFile = file("src/main/antlr/Module.g4")
	inputs.file grammarFile
	outputs.dir generatedGrammar
	main = "org.antlr.v4.Tool"
	args = [
			"-o", "${generatedGrammar}/com/prezi/spaghetti/grammar",
			"-package", "com.prezi.spaghetti.grammar",
			"-no-listener",
			"-visitor",
			grammarFile
	]
	classpath = configurations.antlr
}

processResources {
	def versionMatcher = project.version =~ /^(\d+(?:[\.]\d+)+)(?:-.*)?$/
	def version
	if (versionMatcher) {
		version = versionMatcher[0][1]
	} else {
		logger.warn "Cannot figure out Spaghetti version from \"${version}\", setting to \"unknown\""
		version = "unknown"
	}
	filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: version])
}

idea.module {
	sourceDirs += generatedGrammar
	excludeDirs -= project.buildDir
	if (project.buildDir.exists()) {
		project.buildDir.eachDir {
			if (it != generatedGrammar) {
				excludeDirs += it
			}
		}
	}
}

sourceSets {
    main {
        java {
            srcDir generatedGrammar
        }
    }
}

compileJava {
	dependsOn generateGrammar
}

task run(type: Exec) {
	workingDir "src/test/at"
	commandLine = ["gradle", "clean", "packApplication", "-is"]
}
