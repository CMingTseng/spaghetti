def siteDirectory = file("${buildDir}/site")
def shouldPublish = parent.hasProperty("release") && parent.hasProperty("sonatype")

task cloneSite(type: Exec) {
	outputs.dir siteDirectory
	commandLine "git", "clone", "--branch", "gh-pages", "--depth", "1", "git@github.com:prezi/spaghetti", siteDirectory
	doFirst {
		delete siteDirectory
		mkdir siteDirectory
	}
}

task aggregateJavadoc(type: Javadoc) {
	dependsOn cloneSite

	destinationDir file("${siteDirectory}/javadoc")

	options.docTitle = "Spaghetti ${version} API"
	options.windowTitle = "Spaghetti ${version} API"
	options.header = """<script>
  (function(i,s,o,g,r,a,m){i["GoogleAnalyticsObject"]=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,"script","//www.google-analytics.com/analytics.js","ga");

  ga("create", "UA-54695510-1", "auto");
  ga("send", "pageview");

</script>"""

	parent.javaProjects.each { subproject ->
		source += [subproject.sourceSets.main.allJava]
		classpath += [subproject.sourceSets.main.compileClasspath]
	}

	doFirst {
		// Make sure we don't leave stale HTMLs behind
		delete destinationDir
	}
}

task buildSite {
	dependsOn aggregateJavadoc
}

task addSite(type: Exec) {
	dependsOn buildSite
	onlyIf { shouldPublish }
	commandLine "git", "add", "--all"
	workingDir siteDirectory
}

task commitSite(type: Exec) {
	dependsOn addSite
	onlyIf { shouldPublish }
	commandLine "git", "commit", "--message", "Updated site for ${version} release"
	workingDir siteDirectory
}

task publishSite(type: Exec) {
	dependsOn commitSite
	onlyIf { shouldPublish }
	commandLine "git", "push"
	workingDir siteDirectory
}

tasks.withType(Upload) {
	if (shouldPublish) {
		dependsOn publishSite
	}
}
